apply from: 'lib.gradle'
apply from: 'pomSettings.gradle'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath "com.github.johnrengelman:shadow:8.1.1"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:5.1.0.4882'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'

    apply plugin: 'signing'

    group = 'io.microconfig'
    version = rootProject.version

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            nexus(MavenPublication) {
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', pomDescription)
                    root.children().last() + pomConfig
                }
                from components.java
            }
        }
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.nexus
    }

    dependencies {
        implementation platform(lib.spring_bom)

        compileOnly lib.lombok
        testCompileOnly lib.lombok
        annotationProcessor lib.lombok

        testImplementation lib.junit, lib.mokito
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "microconfig_microconfig"
        }
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }


    test {
        useJUnitPlatform()
        exclude '**/*IT.class'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
}